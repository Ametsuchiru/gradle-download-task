// This example works with Gradle 2.1 and higher. If you need instructions
// for older Gradle versions please have a look at the project's readme file.

/** 
 * Include Ivy into build script classpath. Only necessary for the
 * downloadDirectory task below. If you don't want to download whole
 * directories based on directory listings then just skip this block.
 */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        //only necessary for downloadDirectory task below
        if (gradle.gradleVersion.split("\\.")[0].toInteger() >= 2) {
            classpath 'org.apache.ivy:ivy:2.3.0'
        }
    }
}

/**
 * Include the gradle-download-task plugin
 */
plugins {
    id 'de.undercouch.download' version '1.1'
}

/**
 * Import the Download task. This line is optional.
 * You can of course also always use the full qualified name
 * when you specify a task of type 'Download'.
 */
import de.undercouch.gradle.tasks.download.Download

/**
 * Download a single file to a directory
 */
task downloadFile(type: Download) {
    src 'http://central.maven.org/maven2/org/citationstyles/styles/1.0/styles-1.0.jar'
    dest buildDir
    overwrite true
}

/**
 * Use the extension to download a single file
 */
task downloadFileViaExtension << {
    download {
        src 'http://central.maven.org/maven2/org/citationstyles/styles/1.0/styles-1.0.jar'
        dest buildDir
        overwrite true
    }
}

/**
 * Download multiple files to a directory
 */
task downloadMultipleFiles(type: Download) {
    src([
        'http://central.maven.org/maven2/org/citationstyles/styles/1.0/styles-1.0.jar',
        'http://central.maven.org/maven2/org/eclipse/jetty/jetty-server/9.1.3.v20140225/jetty-server-9.1.3.v20140225-javadoc.jar'
    ])
    dest buildDir
    overwrite true
}

/**
 * Download a file and send a custom header
 */
task downloadWithCustomHeader(type: Download) {
    src 'http://download.oracle.com/otn-pub/java/jdk/8-b132/jre-8-windows-x64.exe'
    dest buildDir
    header 'Cookie', 'oraclelicense=accept-securebackup-cookie'
}

/**
 * Download all files from a directory. Use Ivy's URL lister to
 * read the server's directory listing and then download all files
 * See 'buildscript' instruction above.
 */
task downloadDirectory << {
    def dir = 'http://central.maven.org/maven2/de/undercouch/gradle-download-task/1.0/'
    def urlLister = new org.apache.ivy.util.url.ApacheURLLister()
    def files = urlLister.listFiles(new URL(dir))
    download {
       src files
       dest buildDir
    }
}

/**
 * The following two tasks download a ZIP file and extract its
 * contents to the build directory
 */
task downloadZipFile(type: Download) {
    src 'https://github.com/michel-kraemer/gradle-download-task/archive/1.0.zip'
    dest new File(buildDir, '1.0.zip')
}

task downloadAndUnzipFile(dependsOn: downloadZipFile, type: Copy) {
    from zipTree(downloadZipFile.dest)
    into buildDir
}
