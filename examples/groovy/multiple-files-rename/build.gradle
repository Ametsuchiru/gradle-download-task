/**
 * Include the gradle-download-task plugin
 */
plugins {
    id 'de.undercouch.download' version '5.0.2'
}

/**
 * Define files to download and destination file names
 */
ext.src = [
    'http://git.savannah.gnu.org/gitweb/?p=config.git;a=blob_plain;f=config.guess;hb=HEAD': 'config.guess',
    'http://git.savannah.gnu.org/gitweb/?p=config.git;a=blob_plain;f=config.sub;hb=HEAD': 'config.sub'
]

/**
 * ALTERNATIVE 1: call the download extension in a for loop
 */
task downloadMultipleFiles1 {
    doLast {
        for (s in src) {
            download.run {
                src s.key
                dest new File("$buildDir/alternative1", s.value)
            }
        }
    }
}

/**
 * ALTERNATIVE 2: create multiple tasks
 */
task downloadMultipleFiles2

def i = 0
for (s in src) {
    ++i
    task "downloadMultipleFiles2_${i}"(type: Download) {
        src s.key
        dest new File("$buildDir/alternative2", s.value)
    }
    downloadMultipleFiles2.dependsOn("downloadMultipleFiles2_${i}")
}

defaultTasks 'downloadMultipleFiles1', 'downloadMultipleFiles2'
